;hsqcedetgpsp.3.ptg.2
;avance-version (13/08/29)
;HSQC
;2D H-1/X correlation via double inept transfer
;phase sensitive using Echo/Antiecho-TPPI gradient selection
;with decoupling during acquisition
;using trim pulses in inept transfer
;with multiplicity editing during selection step
;using shaped pulses for inversion on f2 - channel
;for matched sweep adiabatic pulses
;with carbon purge pulse
; .2 version with switched purge pulse active for odd scans only
;if ZGOPNTNS=-Doddpurge
;with BIP proton pulse in T1
;W. Willker, D. Leibfritz, R. Kerssebaum & W. Bermel, Magn. Reson.
;   Chem. 31, 287-292 (1993)
;C. Zwahlen, P. Legault, S.J.F. Vincent, J. Greenblatt, R. Konrat &
;   L.E. Kay, J. Am. Chem. Soc. 119 6711-6721 (1997)
;(R.D. Boyer, R. Johnson & K. Krishnamurthy, 
;   J. Magn. Reson. 165, 253-259 (2003) )
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>


"p2=p1*2"
"d4=1s/(cnst2*4)"
"d11=30m"


"d0=3u"

"in0=inf1/2"


"DELTA=d21-p16-d16-p44-d0*2-p3*2/PI"
"DELTA1=d4-p16-d16-larger(p2,p14)/2-de-8.1u-p3"
"DELTA2=d4-larger(p2,p14)/2"
"DELTA3=d21-p2+p3*2/PI"
"DELTA4=d4-larger(p2,p14)/2-p1*2/PI"
"DELTA5=p3"

"acqt0=0"
baseopt_echo


1 ze
  d11 pl12:f2
2 d1 do:f2 
3 (p1 ph1)
  DELTA2 pl0:f2
  4u
  (center (p2 ph1) (p14:sp3 ph6):f2 )
  4u
  DELTA2 pl2:f2 UNBLKGRAD
  p28 ph1
  4u
  (p1 ph2) 

  (p3 ph3):f2
  d0 
  (p44:sp30 ph5):f1
  d0
  p16:gp1*EA
  d16 pl1:f1
  DELTA
  (p31:sp18 ph1):f2
  (p2 ph1)
  DELTA3
  4u
  (p31:sp18 ph1):f2
  2u
  2u pl2:f2
  (p3 ph4):f2

  (p1 ph1) 
  DELTA4 pl0:f2
  (center (p2 ph1) (p14:sp3 ph1):f2 )
  4u
  p16:gp2
  d16 pl2:f2
  DELTA1 
  4u BLKGRAD
# ifdef oddpurge
   if "nsdone %2 == 0"
     {
     (p3 ph7):f2
     }
  else
     {
     DELTA5
     }
# else
 (p3 ph7):f2
# endif

  0.1u pl12:f2
#   ifdef CPDS
  go=2 ph31 cpds2:f2
#   else
  go=2 ph31 cpd2:f2
#   endif /*CPDS*/
  d1 do:f2 mc #0 to 2 
     F1EA(calgrad(EA), caldel(d0, +in0) & calph(ph3, +180) & calph(ph6, +180) & calph(ph31, +180))
exit
   

ph1=0 
ph2=1
ph3=0 2
ph4=0 0 0 0 2 2 2 2
ph5=0 0 2 2
ph6=0
ph7=0
ph31=2 0 2 0 0 2 0 2
  
;pl0 : 0W
;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;sp3: f2 channel - shaped pulse (180degree inversion)
;spnam3: Crp60,0.5,20.1
;sp18: f2 channel - shaped pulse 180 degree (adiabatic matched sweep)
;spnam18: Crp60_xfilt.2
;sp30: f1 channel - shaped pulse for inversion
;spnam30: BIP720,0.5,20.1
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p14: f2 channel - 180 degree shaped pulse for inversion
;     = 500usec for Crp60,0.5,20.1
;p16: homospoil/gradient pulse
;p28: f1 channel - trim pulse
;p44 : f1 channel - 180 degree shaped pulse for inversion
;p31: f2 channel - 180 degree shaped pulse for adiabatic matched sweep
;                      1730 us at 600.13 MHz, otherwise: 1730*sqrt(600/x)
;d0 : incremented delay (2D)                  [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d4 : 1/(4J)XH
;d11: delay for disk I/O                             [30 msec]
;d16: delay for homospoil/gradient recovery
;d21: set d21 according to multiplicity selection
;        1/(2J(XH))  XH, XH3 positive, XH2 negative
;cnst2: = J(XH)
;inf1: 1/SW(X) = 2 * DW(X)
;in0: 1/(2 * SW(X)) = DW(X)
;nd0: 2
;ns: 1 * n
;ds: >= 16
;td1: number of experiments
;FnMODE: echo-antiecho
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence


;use gradient ratio:	gp 1 : gp 2
;			  80 : 20.1     for C-13
;			  80 :  8.1     for N-15

;for z-only gradients:
;gpz1: 80%
;gpz2: 20.1% for C-13, 8.1% for N-15

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100


                                          ;preprocessor-flags-start
;CPDS: for synchronous CPD start experiment with 
;             option -DCPDS (eda: ZGOPTNS)
                                          ;preprocessor-flags-end



;$Id: hsqcedetgpsp.3,v 1.11 2013/08/30 09:42:07 ber Exp $
