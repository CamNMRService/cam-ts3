;hsqcphsp_homospoil.2
;avance-version (20/03/03)
;HSQC
;2D H-1/X correlation via double inept transfer
;phase sensitive
;with decoupling during acquisition
;with shapes for carbon inverson only
;with shaped proton 180 in t1
;G. Bodenhausen & D.J. Ruben, Chem. Phys. Lett. 69, 185 (1980)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>
#include <Delay.incl>
#include <Grad.incl>

"p2=p1*2"
"p4=p3*2"
"d4=1s/(cnst2*4)"
"d11=30m"


"d0=3u"

"in0=inf1/2"


"DELTA=d0*2+p44"
"DELTA1=d4-p16-d16-(p14/2)"
"DELTA2=d4-p16-d16-p3-de-(p1*2/3.14159)-50u-(p14/2)"

1 ze 
  d11 pl12:f2
2 d11 do:f2
# ifdef PRESAT
  4u pl9:f1
  d1 cw:f1 ph29
  4u do:f1
  4u pl1:f1
#   else
  d1
#   endif /*PRESAT*/ 
  50u UNBLKGRAD
  p16:gp1*-1
  d16
  p16:gp1*-1
  d16
 p16:gp2*-1
  d16
  p16:gp3*-1
  d16
   p16:gp3*-1
  d16
  1m
3 (p1 ph1)
  DELTA1 pl2:f2
  p16:gp1
  d16
  (center (p2 ph2) (p14:sp3 ph9):f2 )
  p16:gp1
  d16 pl2:f2
  DELTA1

#   ifdef TRIMP
  (p28 ph1)
#   endif /* TRIMP */

  (p1 ph3)  
  p16:gp2
  d16
  (p3 ph6):f2
  d0
  (p44:sp30 ph8):f1
  d0
  (p4 ph7):f2
  DELTA pl1:f1
  (p3 ph7):f2
  (p1 ph4) 
  DELTA1
  p16:gp3
  d16
  (center (p2 ph2) (p14:sp3 ph5):f2 )
  p16:gp3
  d16
  50u BLKGRAD
  DELTA2 pl12:f2 
  (p3 ph10):f2
  go=2 ph31 cpd2:f2 
  d11 do:f2 mc #0 to 2 F1PH(calph(ph6, +90) & calph(ph9, +90), caldel(d0, +in0))
exit 
  

ph1=0
ph2=0
ph3=1
ph4=1
ph5=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph6=0 2
ph7=0 0 0 0 2 2 2 2
ph8=0 0 2 2
ph9=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph10=0
ph29=0
ph31=0 2 0 2 2 0 2 0


;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p28: f1 channel -  trim pulse                         [1 msec]
;d0 : incremented delay (2D)                         [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d4 : 1/(4J)XH
;d11: delay for disk I/O                             [30 msec]
;cnst2: = J(XH)
;inf1: 1/SW(X) = 2 * DW(X)
;in0: 1/(2 * SW(X)) = DW(X)
;nd0: 2
;ns: 4 * n
;ds: 16
;td1: number of experiments
;FnMODE: States-TPPI, TPPI, States or QSEQ
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence

;for z-only gradients:
;gpz1: 37.13%
;gpz2: 20.1% 
;gpz3: 13.17%

;use gradient files:   
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100


                                          ;preprocessor-flags-start
;TRIMP: to use trimpulse p28@pl1 start experiment with
;          option -DTRIMP (eda: ZGOPTNS)
                                          ;preprocessor-flags-end



;$Id: hsqcph,v 1.6.2.2 2020/03/03 15:12:09 ber Exp $
