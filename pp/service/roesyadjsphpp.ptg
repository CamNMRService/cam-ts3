;roesyadjsphpp.ptg
;avance-version (10/01/28)
;2D EASY ROESY
;jump-symmetrized with adiabatic spinlocks for mixing
;phase sensitive

;modifications by ptg 15/12/2014:
;with purge pulses before D1
;with gradients unblanked only directly around the gradient pulses
;with gradient prepulse before first RF pulse, equal and opposite first real gradient pulse
; to minimise lock disturbance
;Second gradient pulse removed entirely - => needs 4 scans minimum rather than 2
;unless ZGOPTNS=-DNS2 set

;C.M. Thiele, K. Petzold & J. Schleucher, 
;   Chem. Eur. J. 15, 585-588 (2009)
;J. Schleucher, J. Quant, S. Glaser & C. Griesinger, 
;   J. Magn. Reson A 112, 144-151 (1995)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>
#include <Grad.incl>


define pulse P_SL


"cnst24=1000000.0*tan((cnst28*2*PI)/360.0)/(dw*4)"

"if ( cnst24 > 6500 ) {cnst25 = 6400.0;} else {cnst25 = cnst24;}"
"if ( cnst24 > 6500 ) {cnst29 = atan(cnst25*4*dw/1000000.0)*360.0/(2*PI);} else {cnst29=cnst28;}"

"if (cnst26<cnst25) {cnst27=cnst25;} else {if (cnst26>6500) {cnst27=6400;} else {cnst27=cnst26;} }"


"cnst30=abs(cnst27/tan((cnst29*2*PI)/360.0))"

define list<frequency> roesylist={sfo hz, 0.0, cnst30, -cnst30, 0}


"p30=1000000.0/(cnst27*4)"

"cnst31= (p30/p1) * (p30/p1)"

"spw41=plw1/cnst31"
"spw12=plw1/cnst31"
"spw13=plw1/cnst31"
"spw16=plw1/cnst31"
"spw17=plw1/cnst31"

"cnst23=cnst30+cnst31+p30"


"d11=30m"
"d12=20u"

"p41=1m"


"in0=inf1"

"d0=in0/2-p1*4/3.1416" 


"P_SL=p15/2"


"spoff41=0"
"spoff12=0"
"spoff13=0"
"spoff16=0"
"spoff17=0"
"acqt0=-(p1*2)/3.14159"

1 ze
2 d11
3 d12  roesylist:f1
  4u roesylist.inc
  d12 pl10:f1
  p17 ph5
  p17*2 ph6
  d1
  50u UNBLKGRAD
;# 	ifdef NS2
;  p16:gp2*-1
;#		endif /*NS2*/
  p16:gp1*-1
  d16
  d12 pl1:f1
  (p1 ph1):f1
  d0
  (p1 ph2):f1
  p16:gp1
  d16 roesylist:f1
  4u BLKGRAD
  4u roesylist:f1
  4u roesylist.inc
#   ifdef AV2
  (p41:sp12 ph2):f1
  3u
  (P_SL:sp41 ph2):f1
  3u
  (p41:sp13 ph2):f1
#   else
  (p41:sp12 ph2):f1
  (P_SL:sp41 ph2):f1
  (p41:sp13 ph2):f1
#   endif /*AV2*/
  4u
  4u roesylist:f1
  4u roesylist.inc
#   ifdef AV2
  (p41:sp16 ph2):f1
  3u
  (P_SL:sp41 ph2):f1
  3u
  (p41:sp17 ph2):f1
#   else
  (p41:sp16 ph2):f1
  (P_SL:sp41 ph2):f1
  (p41:sp17 ph2):f1
#   endif /*AV2*/
# 	ifdef NS2
  4u UNBLKGRAD
  p16:gp2
  d16 BLKGRAD
#		endif /*NS2*/
  4u roesylist:f1
  4u roesylist.inc
  4u pl1:f1
  (p0 ph3)
  go=2 ph31 
  d11 mc #0 to 2 F1PH(calph(ph1, +90), caldel(d0, +in0))
exit
  

ph1=0 2
ph2=0
ph3=2 2 0 0 3 3 1 1
ph5=0
ph6=1
ph29=0
ph31=0 2 2 0 1 3 3 1


;pl1 : f1 channel - power level for pulse (default)
;pl9 : f1 channel - power level for presaturation
;sp41: f1 channel - shaped pulse for ROESY-spinlock (= pl1 + cnst31)
;spnam41: Squa100.1000
;sp12: f1 channel - shaped pulse for adiabatic ramp down (lowfield, positive offset) (= pl1 + cnst31)
;spnam12: Gaussramp+down.1
;sp13: f1 channel - shaped pulse for adiabatic ramp up (lowfield, positive offset) (= pl1 + cnst31)
;spnam13: Gaussramp+up.1
;sp16: f1 channel - shaped pulse for adiabatic ramp down (highfield, negative offset) (= pl1 + cnst31)
;spnam16: Gaussramp-down.1
;sp17: f1 channel - shaped pulse for adiabatic ramp up (highfield, negative offset) (= pl1 + cnst31)
;spnam17: Gaussramp-up.1
;p1 : f1 channel -  90 degree high power pulse
;p15: f1 channel - pulse for ROESY spinlock
;p16: homospoil/gradient pulse                         [1 msec]
;p30: f1 channel -  90 degree pulse at sp10
;p41: f1 channel - shaped pulse for adiabatic ramp     [1 msec]
;d0 : incremented delay (2D)
;d1 : relaxation delay; 1-5 * T1
;d11: delay for disk I/O                               [30 msec]
;d12: delay for power switching                        [20 usec]
;d16: delay for homospoil/gradient recovery
;cnst23; (for display purpose only)
;cnst24: min. RF field strength to make sure that the carrier is shifted 
;           to the edge of the spectrum
;cnst25: reduced min. RF field strength in case an upper limit of 6.5kHz is exceeded 
;           (set to 6.4kHz), this leads to a recalculation of the tilt angle (cnst29)
;cnst26: requested RF field strength (gammaB1) for ROESY spinlock
;           reduced to 6.4kHz if an upper limit of 6.5kHz is exceeded
;cnst27: used RF field strength (gammaB1) for ROESY spinlock
;cnst28: requesetd tilt angle for ROESY spinlock (between axis of spinlock and z-axis) [45 degree]
;cnst29: used tilt angle for ROESY spinlock (between axis of spinlock and z-axis) 
;cnst30: low and highfield offset, 
;          calculated from gammaB1 (cnst27) for tilt angle (cnst29)
;cnst31: difference in power level (dB) for spinlock relative to pl1
;inf1: 1/SW = 2 * DW
;in0: 1/(1 * SW) = 2 * DW
;nd0: 1
;NS: 4 * n
;DS: 16
;td1: number of experiments
;FnMODE: States-TPPI, TPPI, States or QSEQ

;for z-only gradients:
;gpz1: 31%

;use gradient files:
;gpnam1: SMSQ10.100


;Processing

;PHC0(F1): 90
;PHC1(F1): -180
;FCOR(F1): 1
 

                                          ;preprocessor-flags-start
;AV2: for AV2 spectrometers set  
;             ZGOPTNS = -DAV2 
;NS2: for extra gradient allowing NS=2 set  
;             ZGOPTNS = -DNS2
                                          ;preprocessor-flags-end


;$Id: roesyadjsphpr,v 1.1.2.1 2010/02/02 15:31:40 ber Exp $
