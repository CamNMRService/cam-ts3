;t1irpg_gradient
;avance-version (12/01/11)
;T1 measurement using inversion recovery
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>
#include <Delay.incl>
#include <Grad.incl>

"p2=p1*2"

"d11=30m"

define list <delay> vdlist = <$VDLIST>

"acqt0=-p1*2/3.1416"


1 ze
  4u do:f2
2 d11
  4u do:f2
  4u pl13:f2
  d1 cpd2:f2
  50u UNBLKGRAD
  p19:gp1*-1
  d16
  4u do:f2
  4u pl12:f2
  4u cpd2:f2
  "TAU=(vdlist-p19-d16)"
  4u pl0:f1
  (p26:sp8 ph1):f1
  p19:gp1 
  d16 BLKGRAD
  TAU pl1:f1
  p1 ph2
  go=2 ph31
  d11 mc #0 to 2 F1QF(calclist(vdlist))
  4u do:f2
exit


ph1=0 2 
ph2=0 0 2 2 1 1 3 3
ph31=0 0 2 2 1 1 3 3


;pl1 : f1 channel - power level for pulse (default)
;p1 : f1 channel -  90 degree high power pulse
;p26 : f1 channel - 180 degree adiabatic 
;d1 : relaxation delay; 1-5 * T1
;d11: delay for disk I/O                             [30 msec]
;vd : variable delay, taken from vd-list
;ns: 1 * n
;ds: 0
;td1: number of experiments = number of delays in vd-list
;FnMODE: QF
;for z-only gradients:
;gpz1: 10%

;define VDLIST

;this pulse program produces a ser-file (PARMOD = 2D)



;$Id: t1ir,v 1.12.8.1 2012/01/31 17:56:37 ber Exp $
